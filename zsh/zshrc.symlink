#!/bin/zsh

## Primary zsh file, delegates to .dotfiles/**/.zsh

# shortcut to this dotfiles path
export DOTFILES=$HOME/.dotfiles

# all of our zsh files
typeset -U config_files
config_files=($DOTFILES/*/*.zsh)

# On how to set PATH in macOS:
# https://gist.github.com/Linerre/f11ad4a6a934dcf01ee8415c9457e7b2

# remove duplicate entries from $PATH
typeset -U PATH path
path=("$HOME/bin" "$DOTFILES/bin" "$HOME/.local/bin" "/opt/homebrew/sbin" "/opt/homebrew/bin" $path)

# load the path files
for file in ${(M)config_files:#*/path.zsh} ; do
  source $file
done

# Lazy-load antidote and generate the static load file only when needed
zsh_plugins=${ZDOTDIR:-$HOME}/.zsh_plugins
if [[ ! ${HOME}/.zsh_plugins.zsh -nt ${HOME}/.dotfiles/zsh/zsh_plugins.txt ]]; then
  (
    source /opt/homebrew/opt/antidote/share/antidote/antidote.zsh
    antidote bundle <${HOME}/.dotfiles/zsh/zsh_plugins.txt >${HOME}/.zsh_plugins.zsh
  )
fi
source ${HOME}/.zsh_plugins.zsh

# zsh-history-substring-search configuration
bindkey '^[[A' history-substring-search-up # or '\eOA'
bindkey '^[[B' history-substring-search-down # or '\eOB'
HISTORY_SUBSTRING_SEARCH_ENSURE_UNIQUE=1

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh} ; do
  source $file
done

autoload -Uz compinit
typeset -i updated_at=$(date +'%j' -r ~/.zcompdump 2>/dev/null || stat -f '%Sm' -t '%j' ~/.zcompdump 2>/dev/null)
if [ $(date +'%j') != $updated_at ]; then
  compinit -i
else
  compinit -C -i
fi

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh} ; do
  source $file
done

# Hermit shell integration
export HERMIT_ROOT_BIN="$(brew --prefix)/bin/hermit"
eval "$(test -x $HERMIT_ROOT_BIN && $HERMIT_ROOT_BIN shell-hooks --print --zsh)"

# Load starship
eval "$(starship init zsh)"

unset config_files

# Skip forward/back a word with opt-arrow
bindkey '[C' forward-word
bindkey '[D' backward-word

# delegate to defaults
test ! -e ~/.profile || source ~/.profile
test ! -e ~/.zprofile || source ~/.zprofile
