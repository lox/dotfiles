
## Primary zsh file, delegates to .dotfiles/**/.zsh

# shortcut to this dotfiles path
export DOTFILES=$HOME/.dotfiles

# all of our zsh files
typeset -U config_files
config_files=($DOTFILES/*/*.zsh)

# zsh has path magic, homebrew bin first
path=(/usr/local/bin /usr/bin /bin /usr/local/sbin /usr/sbin /sbin)

# add arm64 homebrew path too
if [ -d /opt/homebrew ] ; then
  path=(/opt/homebrew/bin "${path[@]}")
fi

# load the path files
for file in ${(M)config_files:#*/path.zsh} ; do
  source $file
done

path=("$DOTFILES/bin" $path)
export PATH

# load zsh, antibody and plugins
source $DOTFILES/zsh/geometry.zsh
source ~/.zsh_plugins.sh

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh} ; do
  source $file
done

autoload -Uz compinit
typeset -i updated_at=$(date +'%j' -r ~/.zcompdump 2>/dev/null || stat -f '%Sm' -t '%j' ~/.zcompdump 2>/dev/null)
if [ $(date +'%j') != $updated_at ]; then
  compinit -i
else
  compinit -C -i
fi

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh} ; do
  source $file
done

unset config_files

# delegate to defaults
test ! -e ~/.profile || source ~/.profile
test ! -e ~/.zprofile || source ~/.zprofile